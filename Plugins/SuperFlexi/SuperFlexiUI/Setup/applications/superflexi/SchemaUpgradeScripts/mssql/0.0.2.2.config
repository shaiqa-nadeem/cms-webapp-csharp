/********************************************************************
 ALTER SelectPageForModuleWithTermAndField
 ********************************************************************/
ALTER PROCEDURE [dbo].[i7_sflexi_items_SelectPageForModuleWithTermAndField]

@ModuleGuid UNIQUEIDENTIFIER,
@PageNumber INT,
@PageSize INT,
@SearchTerm NVARCHAR(max),
@SearchField NVARCHAR(max),
@SortDirection VARCHAR(4)

AS

SELECT
	ItemID,
	TotalRows = Count(*) OVER()
INTO #ItemsToGet
FROM [dbo].[i7_sflexi_values] v
	JOIN (
		SELECT DISTINCT FieldGuid,
			[Name] AS FieldName
		FROM [dbo].[i7_sflexi_fields]
		WHERE Name = @SearchField
	) f on f.FieldGuid = v.FieldGuid
	JOIN i7_sflexi_items items on items.ItemGuid = v.ItemGuid
WHERE FieldValue LIKE '%' + @SearchTerm + '%'
AND v.[ModuleGuid] = @ModuleGuid
ORDER BY 
	CASE WHEN @SortDirection = 'DESC' THEN [SortOrder] END DESC,
	CASE WHEN @SortDirection = 'ASC' THEN [SortOrder] END
OFFSET ((@PageNumber - 1) * @PageSize) ROWS
FETCH NEXT @PageSize ROWS ONLY;

SELECT TotalRows,
	i.*,
	v.FieldValue,
	f.FieldName,
	v.FieldGuid
FROM [dbo].[i7_sflexi_items] i
	JOIN (
		SELECT DISTINCT ItemGuid,
			FieldGuid,
			FieldValue
		FROM [dbo].[i7_sflexi_values]
	) v ON v.ItemGuid = i.ItemGuid
	JOIN (
		SELECT DISTINCT FieldGuid,
			[Name] AS FieldName
		FROM [dbo].[i7_sflexi_fields]
	) f on f.FieldGuid = v.FieldGuid
JOIN #ItemsToGet on #ItemsToGet.ItemID = i.ItemID;
DROP TABLE #ItemsToGet;

/********************************************************************
 END ALTER SelectPageForModuleWithTermAndField
 ********************************************************************/